100 DIM l$(16,16)
110 FOR i=1 TO 16
120 FOR j=1 TO 16
130 LET l$(i,j)="0"
140 IF RND>=0.6 THEN LET l$(i,j)="1"
150 NEXT j
160 NEXT i
170 ORIGIN 4,12
180 CLS;CURSOR 0
190 LET m=LEN l$(1)
200 DIM n$(m,m)
210 FOR i=0 TO m+1
220 PRINT AT i,0;"x"
230 PRINT AT i,m+1;"x"
240 PRINT AT 0,i;"x"
250 PRINT AT m+1,i;"x"
260 NEXT i
270 LET g=0
280 PRINT AT 1,m+3;g
290 FOR i=1 TO m
300 FOR j=1 TO m
310 IF l$(i,j)="0" THEN GOTO 340
320 PRINT AT i,j;"x"
330 GOTO 350
340 PRINT AT i,j;" "
350 NEXT j
360 NEXT i
370 FOR i=1 TO m
380 FOR j=1 TO m
390 LET n=0
400 FOR k=i-1 TO i+1
410 FOR l=j-1 TO j+1
420 IF k=0 OR k>m OR l=0 OR l>m OR (k=i AND l=j) THEN GOTO 440
430 LET n=n+VAL l$(k,l)
440 NEXT l
450 NEXT k
460 LET n$(i,j)=l$(i,j)
470 IF n<=1 OR n>=4 THEN LET n$(i,j)="0"
480 IF n=3 THEN LET n$(i,j)="1"
490 NEXT j
500 NEXT i
510 FOR i=1 TO m
520 LET l$(i)=n$(i)
530 NEXT i
540 LET g=g+1
550 IF INKEY$ <> "" THEN 570
560 GOTO 280
570 CURSOR 1
580 END
